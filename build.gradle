buildscript {
    ext.kotlin_version = '1.1.4-2'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'org.standardout.versioneye' version '1.4.0'
}

apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.mercateo'
version = '0.1.6'
archivesBaseName = "json-schema"

description = """json-schema"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

jacoco {
    toolVersion = "0.7.9"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    required {
        gradle.taskGraph.hasTask('uploadArchives')
    }
    sign configurations.archives
}

if (!hasProperty('sonatypeUsername')) {
    ext.sonatypeUsername = ''
}

if (!hasProperty('sonatypePassword')) {
    ext.sonatypePassword = ''
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'json-schema'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'json-schema generator for the JVM.'
                url 'https://github.com/mercateo/json-schema'

                scm {
                    connection 'scm:git:git@github.com:Mercateo/json-schema.git'
                    developerConnection 'scm:git:git@github.com:Mercateo/json-schema.git'
                    url 'https://github.com/mercateo/json-schema'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'wuerla'
                        name 'Andreas WÃ¼rl'
                        email 'andreas.wuerl@mercateo.com'
                    }
                }
            }
        }
    }
}

def jackson_version = '2.8.8'

dependencies {
    compile group: 'com.googlecode.gentyref', name: 'gentyref', version: '1.2.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jackson_version
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jackson_version
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile(group: 'javax.validation', name: 'validation-api', version: '1.1.0.Final') {
        exclude(module: 'testng')
    }
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.8.0'
    testCompile group: 'com.nhaarman', name: 'mockito-kotlin-kt1.1', version: '1.5.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.9.0'
    testCompile group: 'org.openjdk.jmh', name: 'jmh-core', version: '1.19'
    testCompile group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: '1.19'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test', version: kotlin_version
    testCompile group: 'org.hibernate', name: 'hibernate-validator', version: '5.3.5.Final'

}

kotlin {
    experimental {
        coroutines "enable"
    }
}
