buildscript {
    ext.kotlin_version = '1.5.30'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.github.kt3k.coveralls" version "2.8.4"
}

apply plugin: "kotlin"
apply plugin: "jacoco"
apply plugin: "idea"
apply plugin: "maven"
apply plugin: "signing"

group = "com.mercateo"
version = "0.1.13-SNAPSHOT"
archivesBaseName = "json-schema"

description = """json-schema"""

repositories {
    mavenCentral()
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    required {
        gradle.taskGraph.hasTask("uploadArchives")
    }
    sign configurations.archives
}

if (!hasProperty("sonatypeUsername")) {
    ext.sonatypeUsername = ""
}

if (!hasProperty("sonatypePassword")) {
    ext.sonatypePassword = ""
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name "json-schema"
                packaging "jar"
                // optionally artifactId can be defined here
                description "json-schema generator for the JVM."
                url "https://github.com/mercateo/json-schema"

                scm {
                    connection "scm:git:git@github.com:Mercateo/json-schema.git"
                    developerConnection "scm:git:git@github.com:Mercateo/json-schema.git"
                    url "https://github.com/mercateo/json-schema"
                }

                licenses {
                    license {
                        name "The Apache License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id "wuerla"
                        name "Andreas WÃ¼rl"
                        email "andreas.wuerl@mercateo.com"
                    }
                }
            }
        }
    }
}

def jackson_version = '2.12.5'
def jmh_version = '1.33'

dependencies {
    implementation("com.googlecode.gentyref:gentyref:1.2.0")
    implementation("com.fasterxml.jackson.core:jackson-annotations:${jackson_version}")
    implementation(group: "com.fasterxml.jackson.core", name: "jackson-databind", version: jackson_version)
    implementation(group: "org.slf4j", name: "slf4j-api", version: "1.7.32")
    implementation(group: "javax.validation", name: "validation-api", version: "2.0.1.Final") {
        exclude(module: "testng")
    }
    implementation(group: "org.jetbrains.kotlin", name: "kotlin-stdlib", version: kotlin_version)
    implementation(group: "org.jetbrains.kotlin", name: "kotlin-reflect", version: kotlin_version)

    testImplementation(group: "junit", name: "junit", version: "4.13")
    testImplementation(group: "org.assertj", name: "assertj-core", version: "3.20.2")
    testImplementation(group: "io.mockk", name: "mockk", version: "1.12.0")
    testImplementation(group: "org.openjdk.jmh", name: "jmh-core", version: jmh_version)
    testAnnotationProcessor(group: "org.openjdk.jmh", name: "jmh-generator-annprocess", version: jmh_version)
    testImplementation(group: "ch.qos.logback", name: "logback-classic", version: "1.2.5")
    testImplementation(group: "org.jetbrains.kotlin", name: "kotlin-test", version: kotlin_version)
    testImplementation(group: "org.hibernate", name: "hibernate-validator", version: "6.2.0.Final")
}
